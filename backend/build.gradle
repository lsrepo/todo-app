plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.10'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'org.asciidoctor.jvm.convert' version '4.0.5'
	id 'jacoco'
}

group = 'com.pak'
version = '0.0.1-SNAPSHOT'
description = 'Todo List App'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	integrationTestImplementation {
		extendsFrom testImplementation
	}
	integrationTestRuntimeOnly {
		extendsFrom testRuntimeOnly
	}
}

sourceSets {
	integrationTest {
		java.srcDir 'src/integrationTest/java'
		resources.srcDir 'src/integrationTest/resources'
		compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
		runtimeClasspath += output + compileClasspath
	}
}

repositories {
	mavenCentral()
}

ext {
	set('snippetsDir', file("build/generated-snippets"))
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-websocket'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'io.jsonwebtoken:jjwt-api:0.13.0'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.13.0'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.13.0'
	implementation 'org.springframework.kafka:spring-kafka'
	runtimeOnly 'org.postgresql:postgresql'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.9'
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.boot:spring-boot-testcontainers'
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
	testImplementation 'org.testcontainers:junit-jupiter'
	testImplementation 'org.testcontainers:postgresql'
	testImplementation 'org.testcontainers:kafka'
	testImplementation 'org.springframework.kafka:spring-kafka-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	outputs.dir snippetsDir
	useJUnitPlatform()
}

tasks.register('integrationTest', Test) {
	description = 'Runs integration tests.'
	group = 'verification'
	testClassesDirs = sourceSets.integrationTest.output.classesDirs
	classpath = sourceSets.integrationTest.runtimeClasspath
	useJUnitPlatform()
	shouldRunAfter tasks.named('test')
}

tasks.named('check') {
	dependsOn tasks.named('integrationTest')
}

tasks.named('asciidoctor') {
	inputs.dir snippetsDir
	dependsOn test
}

jacoco {
	toolVersion = '0.8.11'
}

tasks.named('test') {
	finalizedBy tasks.named('jacocoTestReport')
}

def jacocoExclude = [
	'**/com/pak/todo/domain/**',              // domain commands, event payloads
	'**/com/pak/todo/model/dto/**',           // request/response DTOs
	'**/com/pak/todo/model/entity/**',        // JPA entities
	'**/com/pak/todo/model/enums/**',         // enums
	'**/com/pak/todo/web/error/ErrorResponse*.class',
	'**/com/pak/todo/web/error/ResourceNotFoundException*.class'
]

tasks.named('jacocoTestReport', JacocoReport) {
	reports {
		xml.required = true
		html.required = true
		html.outputLocation = layout.buildDirectory.dir('reports/jacoco/test/html')
	}
	// Exclude data classes and DTOs that have no business logic
	classDirectories.setFrom(files(sourceSets.main.output.classesDirs.files.collect {
		fileTree(dir: it, exclude: jacocoExclude)
	}))
}
